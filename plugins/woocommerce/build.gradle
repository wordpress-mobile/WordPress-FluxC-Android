plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.automattic.android.publish-to-s3"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation fluxcProjectDependency

    implementation 'org.apache.commons:commons-lang3:3.7'

    // WordPress libs
    implementation ("org.wordpress:utils:$wordPressUtilsVersion") {
        // Using official volley package
        exclude group: "com.mcxiaoke.volley"
        exclude group: "com.android.support"
    }

    api "org.wordpress:wellsql:$wellSqlVersion"
    kapt "org.wordpress:wellsql-processor:$wellSqlVersion"

    // FluxC annotations
    api fluxcAnnotationsProjectDependency
    kapt fluxcProcessorProjectDependency

    implementation 'com.google.code.gson:gson:2.8.0'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    api "androidx.room:room-ktx:$roomVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
}

project.afterEvaluate {
    publishing {
        publications {
            PluginWoocommercePublication(MavenPublication) {
                from components.release

                groupId "org.wordpress.fluxc.plugins"
                artifactId "woocommerce"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

publishToS3Plugin {
    mavenPublishGroupId "org.wordpress.fluxc.plugins"
    mavenPublishArtifactId "woocommerce"
}
