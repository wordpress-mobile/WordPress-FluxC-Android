pluginManagement {
    repositories {
        exclusiveContent {
            forRepository {
                maven {
                    url = uri("https://a8c-libs.s3.amazonaws.com/android")
                }
            }
            filter {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.configure"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        gradlePluginPortal()
        google()
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.9'
}

def catalogVersion = "1.11.0"
dependencyResolutionManagement {
    repositories {
        exclusiveContent {
            forRepository {
                maven {
                    url = uri("https://a8c-libs.s3.amazonaws.com/android")
                }
            }
            filter {
                includeModule("com.automattic", "dependency-catalog")
            }
        }
    }
    versionCatalogs {
        sharedLibs {
            from("com.automattic:dependency-catalog:$catalogVersion")

            // TODO: Remove the below once https://github.com/Automattic/android-dependency-catalog/pull/20
            //  gets ready for review and the CI is able to publish the new catalog.
            version("androidGradlePlugin", "8.0.2")
            // TODO: Remove the below once https://github.com/Automattic/android-dependency-catalog/pull/20
            //  gets ready for review and the CI is able to publish the new catalog.
            version("kotlin", "1.6.20")
            version("androidx-annotation", "1.0.2")
            version("androidx-appcompat", "1.0.2")
            version("androidx-constraintlayout", "1.1.3")
            version("androidx-recyclerview", "1.0.0")
            version("apache-commons-text", "1.10.0")
            version("facebook-flipper", "0.51.0")
            version("facebook-soloader", "0.9.0")
            version("kotlinx-coroutines", "1.3.9")
        }
    }
}

rootProject.name = 'FluxC'

include ':fluxc',
        ':fluxc-processor',
        ':fluxc-annotations',
        ':plugins:woocommerce',
        ':example',
        ':tests:api'

apply from: './config/gradle/gradle_build_cache.gradle'
