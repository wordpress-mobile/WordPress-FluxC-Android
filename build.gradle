buildscript {
    ext.kotlinVersion = '1.4.10'
    ext.kotlinCoroutinesVersion = '1.3.9'

    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/automattic/maven/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.automattic.android:fetchstyle:1.1'
        classpath 'com.automattic.android:configure:0.3.0'
    }
}

apply plugin: 'com.automattic.android.fetchstyle'
apply plugin: 'com.automattic.android.configure'

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        google()
        jcenter()
        mavenCentral {
            content {
                includeModule("com.goterl", "lazysodium-android")
            }
        }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    // Needed for com.nhaarman:mockito-kotlin-kt1.1
                    // Can possibly be dropped when v2 of mockito-kotlin is released
                    "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                    "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            ]
        }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.31.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

ext {
    daggerVersion = '2.29.1'
    wellSqlVersion = '1.6.0'
    supportLibraryVersion = '27.1.1'
    arch_paging_version = '1.0.1'
    arch_lifecycle_version = '1.1.1'
    arch_core_version = '2.0.1'
    appcompat_version = '1.0.2'
    mockitoVersion = '3.3.3'
}
